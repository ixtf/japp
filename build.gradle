plugins {
    id "idea"
    id "maven-publish"
    id "io.spring.dependency-management" version "1.0.9.RELEASE" apply false
    id "io.freefair.lombok" version "5.3.0" apply false
    id "com.github.johnrengelman.shadow" version "6.1.0" apply false
    id "com.palantir.docker" version "0.25.0" apply false
    id "com.google.protobuf" version "0.8.14" apply false
    id "io.swagger.core.v3.swagger-gradle-plugin" version "2.1.6" apply false
}

subprojects {
    group "com.github.ixtf.japp"
    version "1.0.0"

    apply plugin: "idea"
    apply plugin: "maven-publish"
    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "io.freefair.lombok"

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2" }
        maven { url "https://jitpack.io" }
        maven { url "http://maven.geo-solutions.it" }
    }

    dependencyManagement {
        imports {
            mavenBom "com.github.ixtf.japp:bom:${bomVersion}"
        }
    }

    dependencies {
        testImplementation "io.projectreactor:reactor-tools"
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"
    }

    test {
        useJUnitPlatform()
    }

    java {
        withJavadocJar()
        withSourcesJar()
    }
    javadoc {
        options.addBooleanOption('html5', true)
    }
    // ref https://docs.gradle.org/current/userguide/publishing_maven.html
    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
                versionMapping {
                    usage("java-api") {
                        fromResolutionOf("runtimeClasspath")
                    }
                    usage("java-runtime") {
                        fromResolutionResult()
                    }
                }
                pom {
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "ixtf"
                            name = "Tom King"
                            email = "ixtf1984@gmail.com"
                        }
                    }
                    scm {
                        connection = "scm:git:git://github.com:ixtf/japp.git"
                        developerConnection = "scm:git:ssh://github.com:ixtf/japp.git"
                        url = "https://github.com/ixtf/japp"
                    }
                }
            }
        }
//        repositories {
//            maven {
//                // change URLs to point to your repos, e.g. http://my.org/repo
//                def releasesRepoUrl = "$buildDir/repos/releases"
//                def snapshotsRepoUrl = "$buildDir/repos/snapshots"
//                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
//            }
//        }
    }
}
