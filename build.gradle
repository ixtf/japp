plugins {
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'com.palantir.docker' version "0.20.1"
    id 'com.github.johnrengelman.shadow' version '4.0.3'
}
allprojects {
    group 'com.github.ixtf'
    version '1.5'

    apply plugin: 'idea'
    apply plugin: 'maven'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2' }
        maven { url 'https://jitpack.io' }
    }
}
subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    apply plugin: 'io.franzbecker.gradle-lombok'
    lombok {
        version = "${lombokVersion}"
        sha256 = ""
    }
    
    dependencies {
        compile "org.apache.commons:commons-lang3:3.8.1",
                "org.apache.commons:commons-text:1.6",
                "org.apache.commons:commons-collections4:4.2",
                'commons-io:commons-io:2.6',
                "com.google.guava:guava:${guavaVersion}",
                "ch.qos.logback:logback-classic:${logbackVersion}",
                "ch.qos.logback:logback-access:${logbackVersion}",
                "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}",
                "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:${jacksonVersion}",
                "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${jacksonVersion}",
                "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonVersion}",
                "com.fasterxml.jackson.module:jackson-module-parameter-names:${jacksonVersion}"

        compileOnly "org.projectlombok:lombok:${lombokVersion}",
                "io.vertx:vertx-codegen:${vertxVersion}"

        testCompile "junit:junit:4.12",
                "org.projectlombok:lombok:${lombokVersion}",
                "io.vertx:vertx-unit:${vertxVersion}",
                "io.vertx:vertx-web-client:${vertxVersion}"
    }

    jar.manifest {
        attributes(
                'Implementation-Title': name,
                'Implementation-Version': version,
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Source-Compatibility': sourceCompatibility,
                'Target-Compatibility': targetCompatibility
        )
    }
    [compileJava, compileTestJava].each() {
//        it.options.compilerArgs += ["-Xlint:all", "-Xlint:-options", "-Xlint:-path"]
        it.options.encoding = "UTF-8"
        if (rootProject.hasProperty('failOnWarnings') && rootProject.failOnWarnings.toBoolean()) {
            it.options.compilerArgs += ["-Werror"]
        }
    }
    compileTestJava {
        // serialVersionUID is basically guaranteed to be useless in our tests
        options.compilerArgs += ["-Xlint:-serial"]
    }
    javadoc.options {
        encoding = 'UTF-8'
        use = true
        links 'https://docs.oracle.com/javase/8/docs/api/'
    }
    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    artifacts {
        archives javadocJar, sourcesJar
    }

}
