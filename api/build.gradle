plugins {
    id "application"
    id "io.dotinc.vertx-codegen-plugin"
    id "com.github.johnrengelman.shadow"
    id "com.palantir.docker"
}

dependencyManagement {
    imports {
        mavenBom "io.vertx:vertx-stack-depchain:4.1.6"
    }
}

dependencies {
    annotationProcessor "io.vertx:vertx-codegen:4.1.6:processor"
    annotationProcessor "io.vertx:vertx-service-proxy"
    implementation "ch.qos.logback:logback-classic",
            "com.google.inject:guice",
            "io.vertx:vertx-web-graphql",
            "io.vertx:vertx-service-proxy",
            "io.vertx:vertx-hazelcast",
            "io.vertx:vertx-auth-oauth2",
            "io.vertx:vertx-config-yaml",
            "io.vertx:vertx-health-check",
            "io.vertx:vertx-micrometer-metrics",
            "io.micrometer:micrometer-registry-prometheus",
            "io.jaegertracing:jaeger-client"
    compileOnly "io.vertx:vertx-codegen"
}

codeGen {
    vertxVersion = "4.1.6"
}

mainClassName = "com.github.ixtf.api.ApiLauncher"
jar.manifest {
    attributes(
            'Main-Verticle': "com.github.ixtf.api.MainVerticle",
            'Implementation-Title': name,
            'Implementation-Version': version,
            'Built-By': System.getProperty('user.name'),
            'Built-JDK': System.getProperty('java.version'),
            'Source-Compatibility': sourceCompatibility,
            'Target-Compatibility': targetCompatibility
    )
}

shadowJar {
    mergeServiceFiles()
    archiveName "fat.jar"
//    archiveBaseName.set("fat")
//    archiveClassifier.set("")
//    archiveVersion.set("")
}
docker {
    dependsOn shadowJar
    name "ixtf/${project.name}:${project.version}"
    buildArgs([BUILD_VERSION: "${project.version}"])
    files tasks.shadowJar.outputs
    noCache true
}
